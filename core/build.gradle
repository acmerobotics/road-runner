buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
    }
}

plugins {
    id 'kotlin'
    id 'maven-publish'
}
apply plugin: 'org.jetbrains.dokka'

repositories {
    jcenter()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/kotlin')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    integrationTestImplementation.extendsFrom testImplementation
}

dokka {
    moduleName = 'core'
    outputFormat = 'html'
    outputDirectory = "$buildDir/kdoc"

    includes = ['packages.md']

    jdkVersion = 8

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/acmerobotics/motion-planner/blob/master/core/src/main/kotlin"
        suffix = "#L"
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

dokka.dependsOn dokkaJavadoc

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation "org.apache.commons:commons-math3:3.6.1"

    implementation "com.fasterxml.jackson.core:jackson-databind:2.9.6"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.6"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.6"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.2.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.2.0"

    testImplementation "org.knowm.xchart:xchart:3.5.2"

    integrationTestImplementation "org.junit.jupiter:junit-jupiter-api:5.2.0"
    integrationTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.2.0"

    integrationTestImplementation "org.knowm.xchart:xchart:3.5.2"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task dokkaJar(type: Jar) {
    from dokka
    classifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.acmerobotics.roadrunner'
            artifactId = 'core'
            version = '0.1-SNAPSHOT'
            from components.java
            artifact sourcesJar
            artifact dokkaJar
        }
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

tasks["clean"].doFirst {
    delete file("graphs")
    delete file("csv")
}